1 KEY OFF : CLS : LOCATE 1,1 : PRINT "HAMURABI: Game of Hamurabi - Version 1.01"
2 LOCATE 2,1 : PRINT "Corona Data Systems, Inc."
3 PRINT
10 REM ORIGINAL HAMURABI IN IMSAI 4K BASIC
20 REM THIS VERSION REWRITTEN 12/11/77
30 RANDOMIZE
310 PRINT "HAMURABI - ";
320 PRINT "WHERE YOU GOVERN THE ";
321 PRINT "ANCIENT KINGDOM OF SUMERIA."
330 PRINT "THE OBJECT IS TO FIGURE ";
331 PRINT "OUT HOW THE GAME WORKS!!"
340 PRINT "(IF YOU WANT TO QUIT, ";
341 PRINT "SELL ALL YOUR LAND.)"
400 LET people =100
410 LET births =5
420 LET starved =0
430 LET stores =2800
440 LET rats =200
450 LET yield =3
460 LET harvest =3000
470 LET acres =1000
480 LET J=1
1010 PRINT
1020 PRINT"HAMURABI, I BEG TO REPORT ";
1021 PRINT"THAT LAST YEAR"
1040 PRINT starved ;"PEOPLE STARVED AND";births ;
1041 PRINT "PEOPLE CAME TO THE CITY."
1050 IF J>0 THEN GOTO 1100
1060 LET people =people -INT(people /2)
1070 PRINT "THE PLAGUE KILLED ";
1071 PRINT "HALF THE PEOPLE."
1100 PRINT "THE POPULATION IS NOW";people 
1120 PRINT"WE HARVESTED";harvest ;"BUSHELS ";
1121 PRINT "AT"; yield ; "BUSHELS PER ACRE."
1130 PRINT "RATS DESTROYED";
1131 PRINT rats ; " BUSHELS LEAVING";stores ;
1140 PRINT "BUSHELS IN"
1141 PRINT "THE STOREHOUSES."
1160 PRINT"THE CITY OWNS";acres ;
1161 PRINT "ACRES OF LAND."
1170 LET acrecost=17+INT(6*RND(1))
1180 PRINT"LAND IS WORTH";acrecost;"BUSHELS ";
1181 PRINT"PER ACRE."
1210 PRINT "HAMURABI . . ."
1310 PRINT
1320 PRINT "BUY HOW MANY ACRES";
1330 INPUT buy
1335 PRINT
1340 LET buy=INT(ABS(buy))
1350 IF buy=0 THEN GOTO 1510
1360 LET cost=buy*acrecost
1370 IF cost<=stores  THEN GOTO 1400
1380 GOSUB 9000
1390 GOTO 1310
1400 LET stores =stores -costJ
1410 LET acres =acres +buy
1510 PRINT "SELL HOW MANY ACRES";
1520 INPUT sell
1525 PRINT
1530 LET sell=(ABS(sell))
1540 IF sell=0 THEN GOTO 1710
1550 IF sell<acres  THEN GOTO 1600
1560 IF sell=acres  THEN END
1570 GOSUB 9000
1580 GOTO 1510
1600 LET acres =acres -sell
1610 LET stores =stores +acrecost*sellI
1710 PRINT"HOW MANY BUSHELS SHALL ";
1711 PRINT"WE DISTRIBUTE AS FOOD";
1720 INPUT banquet
1725 PRINT
1730 LET banquet=INT(ABS(banquet))
1740 IF banquet<=stores  THEN GOTO 1770
1750 GOSUB 9000
1760 GOTO 1710
1770 LET stores =stores -banquetI
1780 LET starved =people -INT(banquet/20)
1790 LET births =0
1800 IF starved >=0 THEN GOTO 1910
1810 LET births =-starved /2
1820 LET starved =0
1910 PRINT"HOW MANY ACRES SHALL ";
1911 PRINT "WE PLANT";
1920 INPUT plant
1925 PRINT
1930 LET plant=INT(ABS(plant))
1935 IF plant>acres  THEN GOTO 1960
1940 LET cost=INT(plant/2)
1950 IF cost<=stores  THEN GOTO 1980
1960 GOSUB 9000
1970 GOTO 1910
1980 IF plant>10*people  THEN GOTO 1960
1990 LET stores =stores -cost
2010 LET yield =INT(5*RND(1))+1
2020 LET harvest =yield *plant
2030 LET rats =INT((stores +harvest )*.07*RND(1))
2040 LET stores =stores -rats +harvest 
2050 LET J=INT(10*RND(1))
2060 LET births =INT(births +(5-yield )*stores /600+1)
2070 IF births <=50 THEN GOTO 2100
2080 LET births =50
2100 LET people =people +births -starved 
2110 GOTO 1010
9000 REM***ERROR ROUTINE
9010 PRINT "HAMURABI, THINK AGAIN - ";
9011 PRINT "YOU ONLY HAVE"
9020 PRINT people ;"PEOPLE,";acres ;"ACRES, AND";
9030 PRINT stores ;"BUSHELS IN STOREHOUSES."
9040 RETURN
